{
  parserClass="org.thymeleaf.extras.idea.fragment.selection.parser.FragmentSelectorParser"
  parserUtilClass="org.thymeleaf.extras.idea.fragment.selection.parser.FragmentSelectorParserUtil"

  psiPackage="org.thymeleaf.extras.idea.fragment.selection.psi"
  psiImplPackage="org.thymeleaf.extras.idea.fragment.selection.psi.impl"
  // psiImplUtilClass="org.thymeleaf.extras.idea.fragment.selection.psi.impl.FragmentSelectionPsiImplUtil"

  elementTypeHolderClass="org.thymeleaf.extras.idea.fragment.selection.parser.FragmentSelectorElementTypes"
  elementTypeClass="org.thymeleaf.extras.idea.fragment.selection.parser.FragmentSelectorCompositeElementType"
  tokenTypeClass="org.thymeleaf.extras.idea.fragment.selection.parser.FragmentSelectorElementType"

  implements="org.thymeleaf.extras.idea.fragment.selection.psi.FragmentSelectorPsiCompositeElement"
  extends="org.thymeleaf.extras.idea.fragment.selection.psi.impl.FragmentSelectorPsiCompositeElementImpl"

  tokens=[
    OPERATOR='::'
    OPEN_PARENS='('
    CLOSE_PARENS=')'
    OPEN_ARRAY='['
    CLOSE_ARRAY=']'
    COMMA=','

    string='regexp:([^ \n:(),]+)'
  ]


  mixin("dom_selector")="org.thymeleaf.extras.idea.fragment.selection.psi.impl.DomSelectorImplMixin"
  mixin("template_name")="org.thymeleaf.extras.idea.fragment.selection.psi.impl.TemplateNameImplMixin"
  // implements("dom_selector|template_name")=["com.intellij.psi.ContributedReferenceHost" "org.thymeleaf.extras.idea.fragment.selection.psi.FragmentSelectorPsiCompositeElement"]

  name(".*expr")='expression'
  name(".*expression")='expression'
}

root ::= fragment_selection_expression

fragment_selection_expression ::=  fragment_spec [fragment_paramlist] {
    methods=[
        template_name="/fragment_spec/template_name"
        dom_selector="/fragment_spec/dom_selector"
    ]
}

private fragment_spec ::= (template_name ['::' dom_selector]) | ('::' dom_selector)
template_name         ::= string
dom_selector          ::= string

private fragment_paramlist    ::= '(' param_expr (',' param_expr)* ')' {pin=1}
private param_expr            ::= string